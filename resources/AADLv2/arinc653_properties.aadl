--  Property set for the ARINC653 annex
--  This version comes with the annex draft issued on 20090727

property set ARINC653 is

  Partition_Slots: list of Time applies to (processor);
  
  Slots_Allocation:  list of reference (virtual processor) 
     applies to (processor);
  
  Module_Major_Frame :  Time applies to (processor);
  
  Sampling_Refresh_Period : Time applies to (data port);

  Supported_Error_Code: type enumeration
                  (Module_Config,          -- module level errors
                   Module_Init,
                   Module_Scheduling,
                   Partition_Scheduling,   -- partition level errors
                   Partition_Config,
                   Partition_Handler,
                   Partition_Init,
                   Deadline_Miss,          -- process level errors
                   Application_Error,
                   Numeric_Error,
                   Illegal_Request,
                   Stack_Overflow,
                   Memory_Violation,
                   Hardware_Fault,
                   Power_Fail
                  );
 
  Supported_Partition_Recovery_Action : type enumeration
     (Ignore, Partition_Stop, Warm_Restart, Cold_Restart);

  Supported_Process_Recovery_Action : type enumeration
     (Ignore, Confirm, Partition_Stop, Process_Stop, 
      Process_Stop_And_Start_Another, Process_Restart, 
      Nothing, Cold_Restart, Warm_Restart);

  Supported_Module_Recovery_Action : type enumeration
     (Ignore, Stop, Reset);

  HM_Module_Recovery_Actions : list of    
      ARINC653::Supported_Module_Recovery_Action applies to (processor);

  HM_Partition_Recovery_Actions : list of 
      ARINC653::Supported_Partition_Recovery_Action applies to (virtual processor);

  HM_Process_Recovery_Actions : list of   
      ARINC653::Supported_Process_Recovery_Action applies to (thread);

  --  The difference between ignore and nothing is that ignore does
  --  not perform anything but logs the error. On the contrary,
  --  nothing will do nothing, the HM CallBack should do everything.

  Supported_Access_Type : type enumeration (read, write, read_write);

  Supported_Memory_Type: type enumeration (Data_Memory, Code_Memory, IO_Memory);
  
  HM_Errors   : list of ARINC653::Supported_Error_Code 
     applies to (processor, virtual processor, thread);
  
  HM_Callback : classifier (subprogram Classifier) 
     applies to (thread, virtual processor, processor);
  
  Memory_Type : list of ARINC653::Supported_Memory_Type
    applies to (memory);

  Access_Type : ARINC653::Supported_Access_Type applies to (memory);
  
  Timeout : Time applies to (data port, event data port, event port, data access);
  
  Supported_DAL_Type : type enumeration (LEVEL_A, LEVEL_B, LEVEL_C, LEVEL_D, LEVEL_E);

  DAL : ARINC653::Supported_DAL_Type applies to (virtual processor);

  System_Overhead_Time : Time applies to (processor, virtual processor);

end ARINC653;
